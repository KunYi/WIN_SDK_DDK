// Iczelion's tutorial #9: Controls
//
//	To compile this program use the command line:
//
//	hla -w tut9.hla tut9.rc


program aSimpleWindow;
#include( "w.hhf" )			// Standard windows stuff.
#include( "strings.hhf" )	// Defines HLA string routines.
#include( "memory.hhf" )	// Defines "NULL" among other things.
#include( "args.hhf" )		// Command line parameter stuff.
#include( "conv.hhf" )


static
	hMenu:			dword;
	hInstance:		dword;
	CommandLine:	string;
	hwndButton:		dword;
	hwndEdit:		dword;
	buffer:			char[ 512 ];
	
const
	ButtonID	:= 1;
	EditID		:= 2;
	
	IDM_HELLO	:= 1;
	IDM_CLEAR	:= 2;
	IDM_GETTEXT	:= 3;
	IDM_EXIT	:= 4;
	
	
	AppNameStr	:= "ICZ Tutorial #9";
	MenuNameStr	:= "FirstMenu";
	
readonly

	ClassName:	string := "SimpleWinClass";
	AppName:	string := AppNameStr;
	MenuName:	string := MenuNameStr;
	TestStr:	string := "Wow! I'm in an edit box now";
	


static GetLastError:procedure; external( "__imp__GetLastError@0" );



// The window procedure.  Since this gets called directly from
// windows we need to explicitly reverse the parameters (compared
// to the standard STDCALL declaration) in order to make HLA's
// Pascal calling convention compatible with Windows.
//
// This is actually a function that returns a return result in
// EAX.  If this function returns zero in EAX, then the event
// loop terminates program execution.

procedure WndProc( lParam:dword; wParam:dword; uMsg:uns32; hWnd:dword );
	@nodisplay;
	
begin WndProc;


	// If the WM_DESTROY message comes along, then we've
	// got to post a message telling the event loop that
	// it's time to quit the program.  The return value in
	// EAX must be false (zero).  The GetMessage function
	// will return this value to the event loop which is
	// the indication that it's time to quit.
	
	if( uMsg = w.WM_DESTROY ) then
	
		w.PostQuitMessage( 0 );
		
	elseif( uMsg = w.WM_CREATE ) then
	
		w.CreateWindowEx
		(
			w.WS_EX_CLIENTEDGE,
			"edit",
			NULL,
			w.WS_CHILD | w.WS_VISIBLE | 
				w.WS_BORDER | w.ES_LEFT | w.ES_AUTOHSCROLL,
			50,
			35,
			200,
			25,
			hWnd,
			EditID,
			hInstance,
			NULL
		);
		mov( eax, hwndEdit );
		w.SetFocus( hwndEdit );
		
		w.CreateWindowEx
		(
			NULL,
			"button",
			"My First Button",
			w.WS_CHILD | w.WS_VISIBLE | w.BS_DEFPUSHBUTTON,
			75,
			70,
			140,
			25,
			hWnd,
			ButtonID,
			hInstance,
			NULL
		);
		mov( eax, hwndButton );
		
	
	elseif( uMsg = w.WM_COMMAND ) then
		
		mov( wParam, eax );
		if( lParam = 0 ) then
		
			if( ax = IDM_HELLO ) then
			
				w.SetWindowText( hwndEdit, TestStr );
				w.SendMessage( hwndEdit, w.WM_KEYDOWN, w.VK_END, NULL );
				
			elseif( ax = IDM_CLEAR ) then
			
				w.SetWindowText( hwndEdit, NULL );
				
			elseif( ax = IDM_GETTEXT ) then
			
				w.GetWindowText( hwndEdit, buffer, 512 );
				w.MessageBox
				( 
					NULL, 
					#{ pushd( &buffer ); }#, 
					AppName, 
					w.MB_OK 
				);
				
			else

				w.DestroyWindow( hWnd );

			endif;
			
		else
		
			if( ax = ButtonID ) then
			
				shr( 16, eax );
				if( ax = w.BN_CLICKED ) then
				
					w.SendMessage
					(
						hWnd,
						w.WM_COMMAND,
						IDM_GETTEXT,
						0
					);
					
				endif;
				
			endif;

		endif;
		 
	else
	
		// If a WM_DESTROY message doesn't come along,
		// let the default window handler process the
		// message.  Whatever (non-zero) value this function
		// returns is the return result passed on to the
		// event loop.
		
		w.DefWindowProc( hWnd, uMsg, wParam, lParam );
		exit WndProc;
		
	endif;
	sub( eax, eax );
	
end WndProc;


	
// WinMain-
//
// This is the "main" windows program.  It sets up the
// window and then enters an "event loop" processing
// whatever messages are passed along to that window.
// Since our code is the only code that calls this function,
// we'll use the Pascal calling conventions for the parameters.

procedure WinMain
(
	hInst:dword;
	hPrevInst:	dword;
	CmdLine:	string;
	CmdShow:	dword
);	@nodisplay;

var
	wc:		w.WNDCLASSEX;
	msg:	w.MSG;
	hwnd:	dword;
	
begin WinMain;

	
	// Set up the window class (wc) object:
	
	mov( @size( w.WNDCLASSEX ), wc.cbSize );
	mov( w.CS_HREDRAW | w.CS_VREDRAW, wc.style );
	mov( &WndProc, wc.lpfnWndProc );
	mov( NULL, wc.cbClsExtra );
	mov( NULL, wc.cbWndExtra );
	
	mov( hInstance, wc.hInstance );
	mov( w.COLOR_BTNFACE+1, wc.hbrBackground );
	mov( MenuName, wc.lpszMenuName );
	mov( ClassName, wc.lpszClassName );
	
	// Get the icons and cursor for this application:
	
	w.LoadIcon( NULL, val w.IDI_APPLICATION );
	mov( eax, wc.hIcon );
	mov( eax, wc.hIconSm );
	
	w.LoadCursor( NULL, val w.IDC_ARROW );
	mov( eax, wc.hCursor );
	
	
	// Okay, register this window with Windows so it
	// will start passing messages our way.  Once this
	// is accomplished, create the window and display it.
	
	w.RegisterClassEx( wc );
	
	w.CreateWindowEx
	( 
		w.WS_EX_CLIENTEDGE, 
		ClassName, 
		AppName, 
		w.WS_OVERLAPPEDWINDOW,
		w.CW_USEDEFAULT,
		w.CW_USEDEFAULT,
		300,
		200,
		NULL,
		NULL,
		hInst,
		NULL
	);
	mov( eax, hwnd );
	

	w.ShowWindow( hwnd, w.SW_SHOWNORMAL );
	w.UpdateWindow( hwnd );
	
	// Here's the event loop that processes messages
	// sent to our window.  On return from GetMessage,
	// break if EAX contains false and quit the
	// program.
	
	forever
	
		w.GetMessage( msg, NULL, 0, 0 );
		breakif( !eax );
		w.TranslateMessage( msg );
		w.DispatchMessage( msg );
		
	endfor;
	mov( msg.wParam, eax );
	

end WinMain;

	
begin aSimpleWindow;

	// Get this process' handle:
	
	w.GetModuleHandle( NULL );
	mov( eax, hInstance );
	
	// Get a copy of the command line string passed to this code:
	
	mov( arg.cmdLn(), CommandLine );
	

	
	WinMain( hInstance, NULL, CommandLine, w.SW_SHOWDEFAULT );
	
	// WinMain returns a return code in EAX, exit the program
	// and pass along that return code.
	
	w.ExitProcess( eax );	

end aSimpleWindow;