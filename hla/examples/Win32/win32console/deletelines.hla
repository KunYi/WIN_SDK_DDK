unit consoleUnit;
#include( "win32cons.hhf" );


// deleteLines-
//
//	Deletes "n" lines starting at the current cursor position.

procedure win32cons.deleteLines( n:word ); @nodisplay;
var
	csbi: 	win.CONSOLE_SCREEN_BUFFER_INFO;

begin deleteLines;

	pushad();
	win32cons.info( csbi );
	
	// If we're deleting too many lines, just call clrToEOScrn.

	mov( csbi.dwCursorPosition.Y, ax );
	add( n, ax );

	if( ax >= csbi.dwSize.Y ) then

		win32cons.gotoxy( csbi.dwCursorPosition.Y, 0 );
		win32cons.clrToEOScrn();
		win32cons.gotoxy
		( 
			csbi.dwCursorPosition.Y, 
			csbi.dwCursorPosition.X 
		);

	else

		mov( csbi.dwSize.Y, ax );
		dec( ax );

		mov( csbi.dwSize.X, bx );
		dec( bx );

		win32cons.scrollUpRect
		( 
			n, 
			' ', 
			csbi.wAttributes, 
			csbi.dwCursorPosition.Y,
			0,
			ax,
			bx
		);

	endif;
	popad();			

end deleteLines;



end consoleUnit;