unit consoleUnit;
#include( "stdout.hhf" );
#include( "win32cons.hhf" );








// scrollUp-
//
// Scrolls the screen up one line.

procedure win32cons.scrollUp; @nodisplay;
var
	csbi: 	win.CONSOLE_SCREEN_BUFFER_INFO;
	ci:		win.CHAR_INFO;
	src:	win.small_rect;
	dest:	win.COORD;


begin scrollUp;

	pushad();
	win32cons.info( csbi );

	// Compute the bounds of the rectangle to move.
	// And the destination coordinate to move it to.

	mov( csbi.srWindow.left, ax );
	mov( ax, src.left );
	mov( ax, dest.X );

	mov( csbi.srWindow.top, ax );
	mov( ax, dest.Y );
	inc( ax );
	mov( ax, src.top );

	mov( csbi.srWindow.right, ax );
	mov( ax, src.right );
	mov( csbi.srWindow.bottom, ax );
	mov( ax, src.bottom );

	// Set up the fill char and attribute:

	mov( ' ', ci.AsciiChar );
	mov( csbi.wAttributes, ax );
	mov( ax, ci.Attributes );

	// Okay, scroll the screen up one line:

	win.ScrollConsoleScreenBuffer
	(
		ci,
		#{
			pushd( (type dword dest));
		}#,
		0,	// NULL clipping region.
		src,
		stdout.handle()
	);
	popad();	

end scrollUp;








end consoleUnit;