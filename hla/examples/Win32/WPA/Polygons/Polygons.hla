// Polygons.hla-
//
//	Program that demonstrates the use of the w.Polygon and w.SetPolyFillMode
// functions.
//
// Note: this is a unit because it uses the WinMail library module that
//		 provides a win32 main program for us.

unit Polygons;

#includeonce( "rand.hhf" )
#includeonce( "memory.hhf" )
#includeonce( "math.hhf" )
#includeonce( "w.hhf" )
#includeonce( "wpa.hhf" )
#includeonce( "winmain.hhf" )

?@NoDisplay := true;
?@NoStackAlign := true;
	
static
	ClientSizeX			:int32 := 0;	// Size of the client area
	ClientSizeY			:int32 := 0;	//  where we can paint.
	
readonly

	ClassName	:string := "PolygonsWinClass";		// Window Class Name
	AppCaption	:string := "Polygons Program";		// Caption for Window

	
	// The dispatch table:
	//
	//	This table is where you add new messages and message handlers
	//	to the program.  Each entry in the table must be a MsgProcPtr_t
	//	record containing two entries: the message value (a constant,
	//	typically one of the w.WM_***** constants found in windows.hhf)
	//	and a pointer to a "MsgProcPtr_t" procedure that will handle the
	//	message.
 
	
	Dispatch	:MsgProcPtr_t; @nostorage;

		MsgProcPtr_t	
			MsgProcPtr_t:[ w.WM_DESTROY, &QuitApplication	],
			MsgProcPtr_t:[ w.WM_PAINT,   &Paint				],
			MsgProcPtr_t:[ w.WM_SIZE,	 &Size				],
			
			// Insert new message handler records here.
			
			MsgProcPtr_t:[ 0, NULL ];	// This marks the end of the list.
			

	
/**************************************************************************/
/*          W I N M A I N   S U P P O R T   C O D E				          */
/**************************************************************************/

// initWC - We don't have any initialization to do, so just return:

procedure initWC; @noframe;
begin initWC;

	ret();

end initWC;

// appCreateWindow-	the default window creation code is fine, so just
//					call defaultCreateWindow.

procedure appCreateWindow; @noframe;
begin appCreateWindow;

	jmp defaultCreateWindow;

end appCreateWindow;


// This is the custom message translation procedure.
// We're not doing any custom translation, so just return EAX=0
// to tell the caller to go ahead and call the default translation
// code.

procedure LocalProcessMsg( var lpmsg:w.MSG ); 
begin LocalProcessMsg;

	xor( eax, eax );

end LocalProcessMsg;
			

// appException-
//
// Gives the application the opportunity to clean up before
// aborting when an unhandled exception comes along:

procedure appException( theException:dword in eax );
begin appException;

	raise( eax );

end appException;

	
/**************************************************************************/
/*          A P P L I C A T I O N   S P E C I F I C   C O D E	          */
/**************************************************************************/


// QuitApplication:
//
//	This procedure handles the w.WM_DESTROY message.
//	It tells the application to terminate.  This code sends
//	the appropriate message to the main program's message loop
//	that will cause the application to terminate.
	
procedure QuitApplication( hwnd: dword; wParam:dword; lParam:dword );
begin QuitApplication;

	w.PostQuitMessage( 0 );

end QuitApplication;




// Paint:
//
//	This procedure handles the w.WM_PAINT message.

procedure Paint( hwnd: dword; wParam:dword; lParam:dword );
var
	hdc			:dword;			// Handle to video display device context.
	ps			:w.PAINTSTRUCT;	// Used while painting text.
	windingPoly	:w.POINT[8];	// Holds the points for two overlapping rects.
	alterPoly	:w.POINT[8];	// Holds the points for two overlapping rects.
	
	#macro index(i);
		(i*@size(w.POINT))
	#endmacro
	

begin Paint;


	// Message handlers must preserve EBX, ESI, and EDI.
	// (They've also got to preserve EBP, but HLA's procedure
	// entry code already does that.)
	
	push( ebx );
	push( esi );
	push( edi );
	
	// Fill in the polygon array to form two overlapping rectangles:
	
	mov( 0, windingPoly.x[ index(7) ] );
	
	mov( ClientSizeX, eax );
	shr( 2, eax );	// 1/4 of the window's width
	mov( eax, windingPoly.x[ index(5) ] );
	mov( eax, ecx );
	
	shr( 1, eax );	// 1/8 of the window width
	mov( eax, windingPoly.x[ index(0) ] );
	mov( eax, windingPoly.x[ index(3) ] );
	mov( eax, windingPoly.x[ index(4) ] );
	mov( eax, windingPoly.x[ index(6) ] );

	add( ecx, eax );	// 3/8 of the window's width	
	mov( eax, windingPoly.x[ index(1) ] );
	mov( eax, windingPoly.x[ index(2) ] );
	
	
	mov( 5, eax );
	mov( eax, windingPoly.y[ index(0) ] );
	mov( eax, windingPoly.y[ index(1) ] );
	mov( eax, windingPoly.y[ index(4) ] );

	mov( ClientSizeY, eax );
	shr( 1, eax );
	mov( eax, ecx );
	shr( 1, ecx );
	add( ecx, eax );	// 3/4 of ClientSizeY
	mov( eax, windingPoly.y[ index(2) ] );
	mov( eax, windingPoly.y[ index(3) ] );
	mov( eax, windingPoly.y[ index(6) ] );
	
	shr( 1, eax );	// 3/8 of ClientSizeY
	mov( eax, windingPoly.y[ index(5) ] );
	mov( eax, windingPoly.y[ index(7) ] );
	

	// Create the alterPoly array here by offseting the
	// winding polygon by half the screen:
	
	mov( ClientSizeX, ecx );
	shr( 1, ecx );
	for( xor( esi, esi ); esi < 8; inc( esi )) do
	
		mov( windingPoly.x[ esi*8], eax );
		add( ecx, eax );
		mov( eax, alterPoly.x[ esi*8 ] );
		
		mov( windingPoly.y[ esi*8], eax );
		mov( eax, alterPoly.y[ esi*8 ] );
		
	endfor;			
	

	// Note that all GDI calls must appear within a 
	// BeginPaint..EndPaint pair.
	
	BeginPaint( hwnd, ps, hdc );

		SelectObject( w.GetStockObject( w.GRAY_BRUSH ));
		
		// Draw the curves we've created:
		
		SetPolyFillMode( w.ALTERNATE );
		Polygon( windingPoly, 8 );
		
		SetPolyFillMode( w.WINDING );
		Polygon( alterPoly, 8 );		

	EndPaint;

	
	pop( edi );
	pop( esi );
	pop( ebx );

end Paint;


// Size-
//
//	This procedure handles the w.WM_SIZE message
//	Basically, it just saves the window's size so
//	the Paint procedure knows when a line goes out of
//	bounds.
//
//	L.O. word of lParam contains the new X Size
//	H.O. word of lParam contains the new Y Size

procedure Size( hwnd: dword; wParam:dword; lParam:dword );
begin Size;

	// Convert new X size to 32 bits and save:

	movzx( (type word lParam), eax );
	mov( eax, ClientSizeX );

	// Convert new Y size to 32 bits and save:

	movzx( (type word lParam[2]), eax );
	mov( eax, ClientSizeY );
	
	xor( eax, eax ); // return success.
	

end Size;
				
end Polygons;
 